/*
 * Copyright (C) 2007, The EROS Group, LLC
 *
 * This file is part of the Coyotos Operating System.
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2,
 * or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
 */

/*
 * To create SpaceBanks and allocate storage from them, mkimage provides
 * built-in functions, (new Bank(), new Page(), etc.), along with a
 * built-in constant, PrimeBank, the root of the bank hierarchy.
 *
 * However, unless this module is imported into an image, any invocation of
 * a SpaceBank capability will hang.
 *
 * coyotos.SpaceBank has only one exported interface for public consumption:
 *
 *   Verifier:  The global SpaceBank verifier.
 * 
 * The *important* side-effect of including this module is to create
 * the SpaceBank process, which uses its Range Key to process the 
 * allocation and bank data mkimage places in the output image.  It then
 * walks through every bank endpoint created by mkimage, and places itself
 * as the reciever.  This allows invocations of bank capabilities to succeed.
 */
module coyotos.SpaceBank {
   import TargetInfo = coyotos.TargetInfo;
   import rt = coyotos.RunTime;

  /* Allocate a process, set up its capRegs, address space, and make it 
   * running. 
   *
   * bank should be a freshly created bank.
   *
   * First, a process consists of:
   *   1. A process
   *   2. One reply endpoint, w/ epID 0 and PM set to 1
   *   3. One initial endpoint, w/ epID 1 and PM cleared.
   *
   * The REPLYEPT, SELF, and INITEPT capability registers are set up to link
   * all of these together.  The SPACEBANK capability register is set to
   * the passed in bank.
   *
   *   4. image.space is installed as the address space
   *   5. proc.faultCode is set to PROCESS_FC_STARTUP
   *   6. proc.faultInfo is set to image.pc
   */
   export def make_running_process_with_as(bank, as, pc) {
     def proc = new Process(bank);

     def replyept = new Endpoint(bank);
     replyept.pm = 1;
     replyept.recipient = proc;
     replyept.endpointID = 0;

     def initept = new Endpoint(bank);
     initept.recipient = proc;
     initept.endpointID = 1;

     proc.capReg[rt.REG.REPLYEPT] = replyept;
     proc.capReg[rt.REG.SELF] = proc;
     proc.capReg[rt.REG.SPACEBANK] = bank;
     proc.capReg[rt.REG.INITEPT] = initept;

     proc.schedule = NullCap(); /** @bug this should be a schedule cap */
     proc.addrSpace = as;
     proc.faultCode = rt.PROCESS_FC_STARTUP;
     proc.faultInfo = pc;

     return proc;
   }
   export def make_running_process(bank, image) {
     return make_running_process_with_as(bank, image.space, image.pc);
   }

   /* The Spacebank verifier is a PrimeBank endpoint with all restrictions */
   export def Verifier = enter(PrimeBankEndpoint, 0xffffffff);

   /* load the SpaceBank process image, and install a stack page. */
   def image = loadimage(PrimeBank, "coyotos/SpaceBank");
   image.space = insert_subspace(PrimeBank,
				 image.space,
				 noexec(new Page(PrimeBank)),
				 TargetInfo.small_stack_page_addr);

   def spacebank = make_running_process(PrimeBank, image);

   /* Register assignments for SpaceBank; only exported so that they can
    * be used by spacebank.c.
    */
   export capreg APP {
     ADDRSPACE = rt.REG.APP0,
     RANGE,	     /* Range key */
     KERNLOG,	    /* log key for debugging output */
     INITGPT,        /* initial GPT for bootstrapping */
     TMP1,
     TMP2,
     TMP3,
     TMPPAGE
   };

   /* Set up spacebank's application registers */
   spacebank.capReg[APP.RANGE] = Range();
   spacebank.capReg[APP.KERNLOG] = KernLog();
   spacebank.capReg[APP.INITGPT] = new GPT(PrimeBank);
}
