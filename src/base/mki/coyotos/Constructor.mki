/*
 * Copyright (C) 2007, The EROS Group, LLC
 *
 * This file is part of the Coyotos Operating System.
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2,
 * or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
 */

/*
 * coyotos.Constructor exports the following interfaces:
 *
 *   MetaConstructor:
 *     A Constructor that creates Builders, which are used to create new
 *     Constructors.
 *
 *   Verifier:  The global Constructor verifier.
 *
 *   create_small(bank, file, handler):
 *     Creates a new, pre-Sealed constructor which creates processes based
 *     on *file*, which must have been linked with the -small-space GCC 
 *     option.  *handler* should either be NullCap(), or a constructor for
 *     the desired error handler.
 * 
 *     The constructor will be created in a new child bank of *src_bank*.
 *     
 *     The returned value is a Constructor capability to the new constructor.
 *
 *   create(src_bank, as, pc, handler):
 *     Creates a new, pre-Sealed constructor.  *as* should be a constructor
 *     for the address space needed, and *pc* is the initial PC for the 
 *     program.  *handler* is as in create_small().
 * 
 *     The constructor will be created in a new child bank of *src_bank*.
 *     
 *     The returned value is a Constructor capability to the new constructor.
 *
 *   set_tool(con, addr, value):
 *     Sets up a tool for the Yield of a *con*, a constructor created with
 *     create() or create_small().  The tool at address *addr* is set to 
 *     *value*.
 */
module coyotos.Constructor {
  import sb = coyotos.SpaceBank;

  import llutil = coyotos.LowLevelUtil;
  import rt = coyotos.RunTime;
  import small = coyotos.model.Small;

  /* Our protected payload values */
  export enum PP {
    Builder = 0,
    Constructor = 1,
    Verifier = 2,
    BRAND = 0xffff  /* used to brand processes */
  };  
  
  /* Our application registers. */
  export capreg APP {
    /* The YIELD_* registers are those used by the Constructor interfaces
     * to create new yield or verify the existing of the constructor.  For
     * pre-sealed Constructors, these are set up by setup_sealed(), below.
     *
     * The initialize() routine in Constructor.c notices that we're pre-sealed
     * by the fact that the YIELD_BRAND register is non-Null.
     */
    YIELD_BRAND = rt.REG.APP0,  /* The Brand for our yield */
    YIELD_TOOLS,		/* read-only cap to Tools CapPage */
    YIELD_PROTOSPACE,		/* weak cap to ProtoSpace page */
    YIELD_ADDRSPACE,		/* The address space (or constructor) */
    YIELD_HANDLER,		/* The Handler (or constructor thereof) */

    /* The BUILD_* registers are those used by the Builder interfaces to
     * modify the Tools and ProtoSpace the process is started with.  When
     * the Builder is seal()ed, these are overwritten with Null capabilities
     * to prevent accidental modification.
     */
    BUILD_TOOLS,	/* writable Tools capPage cap, NULL when sealed */
    BUILD_PROTOSPACE,   /* writable ProtoSpace Page cap, NULL when sealed */

    /* A copy of the Discrim cap, pulled out of our Tools at initialization */
    DISCRIM,

    /* Temporary cap registers.
     * 
     * Used during Constructor.create(), initialization, etc.
     */
    NEW_PROC,
    NEW_ENDPT,
    NEW_RENDPT,
    TMP
  };
  
  /* The initial app registers as seen by ProtoSpace. These overlap those
   * which may be smashed by runtime initialization.
   */
  export capreg PROTOAPP {
    ADDRSPACE = rt.REG.APP_SMASHED0,
    HANDLER,
    SCHEDULE
  };

  /* Protospace is the primordial address space for a process.  It fits
   * exactly in a single page.  The target initial PC is stored as a 64-bit
   * integer at the beginning of the page, and the ProtoSpace entry point
   * is immediately afterwards, at offset 8.
   *
   * To set up a protospace for use, a new page is allocated, the
   * original protospace is copied into it, and the needed start PC
   * is written into it at offset 0.
   */
  def protospace_image = loadimage(PrimeBank, "coyotos/ProtoSpace");
  def protospace = weaken(protospace_image.space);
  
  /* The Constructor program image.
   */
  def cons_image = small.load_image(PrimeBank, "coyotos/Constructor");

  /* There's a chicken-and-egg problem with the metaconstructor; we need
   * to know its brand before we can create any constructors, but the
   * metaconstructor is itself a constructor. 
   *
   * So we allocate its process early, and set it up late, to get around
   * the issue.
   */
  def metacon_proc = llutil.make_running_process(PrimeBank, cons_image);
 
  /* The constructor brand is a distinguished entry cap to
   * the MetaConstructor. 
   */
  def constructor_brand = enter(metacon_proc.capReg[rt.REG.INITEPT],
				PP.BRAND);
 
  /* The Verifier key is just a Verifier entry cap to the MetaConstructor. */ 
  export def Verifier = enter(metacon_proc.capReg[rt.REG.INITEPT], 
			      PP.Verifier);
 
  /* The default tools everyone starts with. */ 
  def primordial_tools = readonly(new CapPage(PrimeBank));
  primordial_tools[rt.TOOL.DISCRIM] = Discrim();
  primordial_tools[rt.TOOL.PROTOSPACE] = protospace;
  primordial_tools[rt.TOOL.CONSTRUCTOR_VERIFY] = Verifier;
  primordial_tools[rt.TOOL.AUDIT_LOG] = NullCap();

  export def fresh_tools(bank) {
    return dup(bank, primordial_tools);
  }

  /* Make a running process from an AS and PC.
   *
   * *bank* should be a fresh bank.  *as_cons* should be the desired
   * address space, or a constructor for a SpaceHandler object which can
   * provide the desired address space. *handler* should be NullCap() or
   * a constructor for the desired Process handler.
   *
   * The process will be started up with the default Tools node;  if
   * other tools are needed, use:
   *
   *   import rt = coyotos.RunTime;
   *   import cons = coyotos.Constructor;
   *   ...
   *   def proc = cons.make_process(...);
   *   ...
   *   proc.capReg[rt.REG.TOOLS][slot] = value;
   *
   * to add them to the tools array.
   */
  export def make_process_from_as(bank, as_cons, pc, handler_cons) {
    def my_protospace = dup(bank, protospace);

    set_page_uint64(my_protospace, 0, pc);

    def proc = llutil.make_running_process_from_as(bank, 
						   weaken(my_protospace), 8);

    proc.capReg[rt.REG.TOOLS] = readonly(fresh_tools(bank));
    
    proc.capReg[PROTOAPP.ADDRSPACE] = as_cons;
    proc.capReg[PROTOAPP.HANDLER] = handler_cons;
    proc.capReg[PROTOAPP.SCHEDULE] = proc.schedule;

    return proc;
  }

  /* Make a running process from an image.
   *
   * *bank* should be a fresh bank.  *image* should be the result of a
   * coyotos.model.* "load_image()" call. *handler* should be NullCap() or
   * a constructor for the desired Process handler.
   *
   * The process will be started up with the default Tools node;  if
   * other tools are needed, use:
   *
   *   import rt = coyotos.RunTime;
   *   import cons = coyotos.Constructor;
   *   ...
   *   def proc = cons.make_process(...);
   *   ...
   *   proc.capReg[rt.REG.TOOLS][slot] = value;
   *
   * to add them to the tools array.
   */
  export def make_process(bank, image, handler) {
    return make_process_from_as(bank, image.space, image.pc, handler);
  }

  /*
   * Internal function which sets up *proc* (a running process with the
   * constructor image) to be a sealed constructor of image *as*, with
   * start PC *pc*, handler *handler*, all using storage from *bank*.
   * *proc* must have been allocated from *bank*.
   *
   * set_tool() can be used to set up its tool page.
   */
  def setup_sealed_constructor(bank, proc, as, pc, handler) {
    def my_tools = fresh_tools(bank);
    def my_protospace = dup(bank, protospace);
    
    def initEpt = proc.capReg[rt.REG.INITEPT];
    
    set_page_uint64(my_protospace, 0, pc);
    
    proc.brand = constructor_brand;
    
    /* Set up the base capability registers not initialized by 
     * llutil.make_bare_process()
     */
    proc.capReg[rt.REG.TOOLS] = primordial_tools;
    proc.capReg[rt.REG.RUNTIME] = NullCap();

    /* Set up the YIELD app registers for the Constructor so that it
     * will initialize as Sealed.
     */
    proc.capReg[APP.YIELD_TOOLS] = readonly(my_tools);
    proc.capReg[APP.YIELD_PROTOSPACE] = weaken(my_protospace);
    proc.capReg[APP.YIELD_ADDRSPACE] = as;
    proc.capReg[APP.YIELD_HANDLER] = handler;
    proc.capReg[APP.YIELD_BRAND] = enter(initEpt, PP.BRAND);

    /* Return the Constructor facet */
    return enter(initEpt, PP.Constructor);
  }
  
  /* Creates a constructor using *bank* (which should be a fresh bank).
   * The constructor will have an address space made from *as*, with an
   * initial PC of *pc*, and a handler of *handler*.
   *
   * Both *as* and *handler* can be constructors; in this case, when the
   * program is constructed, a new Address Space or Handler will be 
   * constructed using *as* and *handler*.
   *
   * set_tool() can be used to set up the yield's tool page.
   */
  export def make_from_as(bank, as, pc, handler) {
    def proc = llutil.make_running_process(bank, cons_image); 
    
    return setup_sealed_constructor(bank, proc, as, pc, handler);
  }
  
  /* Creates a constructor using *bank* (which should be a fresh bank).
   * The constructor will create processes made from *image*, which should
   * be generated by a coyotos.model.* "load_image()" call.  The created
   * processes will have a handler of *handler*.
   *
   * Both *handler* can be a constructor; in this case, when the
   * program is constructed, a new Handler will be constructed using *handler*.
   *
   * set_tool() can be used to set up the yield's tool page.
   */
  export def make(bank, image, handler) {
    return make_from_as(bank, image.space, image.pc, handler);
  }

  /* Sets the tool at offset *offset* in the tools space for the yield of
   * *con* to *value*.
   */
  export def set_tool(con, offset, value) {
    con.recipient.capReg[APP.YIELD_TOOLS][offset] = value;
  }

  /* Gets the bank a constructor is using */
  export def get_bank(con) {
    return con.recipient.capReg[rt.REG.SPACEBANK];
  }
  
  /* Now we can set up the metaconstructor;  It's just a constructor whose
   * yield runs the constructor program.
   *
   * Because of the way we set up constructor_brand, it looks like a child
   * of itself.  So Verifier.verifyYield(MetaConstructor) will succeed.
   */
  export def MetaConstructor = 
    setup_sealed_constructor(PrimeBank, metacon_proc, 
			     cons_image.space, cons_image.pc, NullCap());
}
